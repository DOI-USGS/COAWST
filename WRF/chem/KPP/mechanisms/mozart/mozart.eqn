#EQUATIONS {MOZART, check troee, usr5, usr17, rc_n2o5 }
// photorates
 {001:J01} M{=O2}+hv=O+O  		: .20946_dp*j(Pj_o2)  ;
 {002:J02} O3+hv=O1D_CB4{+O2}  		: j(Pj_o31d) ;
 {003:J03} O3+hv=O{+O2}  		: j(Pj_o33p) ;
 {004:J04} N2O+hv=O1D_CB4{+N2} 	        : j(Pj_n2o)  ;
 {005:J05} NO2+hv=O+NO  		: j(Pj_no2)  ;
 {006:J06} N2O5+hv=NO2+NO3	        : j(Pj_n2o5) ;
 {007:J07} HNO3+hv=OH+NO2 		: j(Pj_hno3) ;
 {009:J09} NO3+hv=.89 NO2+ .11 NO + .89 O3: j(Pj_no3o) ; 
 {010:J10} HO2NO2+hv=0.66 HO2+0.66 NO2+0.33 OH+0.33 NO3 : j(Pj_hno4) ;
 {011:J11} CH3OOH+hv=CH2O+HO2+OH        : j(Pj_ch3o2h) ;
 {012:J12} CH2O+hv=HO2+HO2+CO 		: j(Pj_ch2or) ;
 {013:J13} CH2O+hv=CO+H2 		: j(Pj_ch2om) ;
 {014:J14} H2O2+hv=OH+OH 		: j(Pj_h2o2) ;
 {015:J15} CH3CHO+hv=CH3O2+CO+HO2       : j(Pj_ch3cho) ;
 {016:J16} POOH+hv=CH3CHO+CH2O+HO2+OH   : j(Pj_pooh) ;
 {017:J17} CH3COOOH+hv=CH3O2+OH{+CO2}   : .28_dp*j(Pj_h2o2) ;
 {018:J18} PAN+hv=0.6 CH3CO3+0.6 NO2+0.4 CH3O2+0.4 NO3{+0.4 CO2} : j(Pj_pan) ;
 {019:J19} MPAN+hv=MCO3+NO2             : j(Pj_pan) ;
 {020:J20} MACR+hv=0.67 HO2+0.33 MCO3+0.67 CH2O+0.67 CH3CO3+0.33 OH+0.67 CO: j(Pj_macr) ;
 {021:J21} MVK+hv=0.7 C3H6+0.7 CO+0.3 CH3O2+0.3 CH3CO3 : j(Pj_mvk) ;
 {022:J22} C2H5OOH+hv=CH3CHO+HO2+OH     : j(Pj_ch3o2h) ;
 {023:J23} C3H7OOH+hv=0.82 CH3COCH3+HO2+OH  : j(Pj_ch3o2h) ;
 {024:J24} ROOH+hv=CH3CO3+CH2O+OH       : j(Pj_ch3o2h) ;
 {025:J25} CH3COCH3+hv=CH3CO3+CH3O2     : j(Pj_ch3coch3) ;
 {026:J26} CH3COCHO+hv=CH3CO3+CO+HO2    : j(Pj_ch3cocho) ;
 {027:J27} XOOH+hv=OH                   : j(Pj_ch3o2h) ;
 {028:J28} ONITR+hv=HO2+CO+NO2+CH2O     : j(Pj_ch3cho) ;
 {029:J29} ISOPOOH+hv=.402 MVK+.288 MACR+.69 CH2O+HO2: j(Pj_ch3o2h) ;
 {030:J30} HYAC+hv=CH3CO3+HO2+CH2O      : j(Pj_hyac) ;
 {031:J31} GLYALD+hv=HO2+HO2+CO+CH2O    : j(Pj_glyald) ;
 {032:J32} MEK+hv=CH3CO3+C2H5O2         : j(Pj_mek) ;
 {033:J33} BIGALD+hv=0.45 CO+0.13 GLYOXAL+0.56 HO2 +0.13 CH3CO3+ 0.18 CH3COCHO : .2_dp*j(Pj_no2) ;
 {034:J34} GLYOXAL+hv=CO+CO+HO2+HO2     : j(Pj_gly) ;
 {035:J35} ALKOOH+hv=0.4 CH3CHO+0.1 CH2O+0.25 CH3COCH3+0.9 HO2+0.8 MEK+OH: j(Pj_ch3o2h) ;
 {036:J36} MEKOOH+hv=OH+CH3CO3+CH3CHO   : j(Pj_ch3o2h) ;
 {037:J37} TOLOOH+hv=OH+0.45 GLYOXAL+0.45 CH3COCHO+0.9 BIGALD : j(Pj_ch3o2h) ;
 {038:J38} TERPOOH+hv=OH+0.1 CH3COCH3+HO2+MVK+MACR : j(Pj_ch3o2h) ;
// gas phase reactions
 { 39:001 } O+M{=O2}=O3                    : 0.20946*(C_M *6.00e-34_dp*(TEMP/300._dp)**(-2.3_dp)) ;
 { 40:002 } O+O3=M{=O2+O2}                 : ARR2(8.0e-12_dp, 2060.0_dp, TEMP);
 { 41:003 } O1D_CB4+M{=O2,N2}=O{+O2,N2}    : .79_dp*ARR2(2.1e-11_dp, -115.0_dp, TEMP) +  .21_dp*ARR2(3.2e-11_dp, -70.0_dp, TEMP);
 { 42:004 } O1D_CB4+H2O=OH+OH              : 2.2e-10_dp ;
 { 43:005 } O1D_CB4+H2=HO2+OH              : 1.1e-10_dp ;
 { 44:006 } OH+H2=HO2+H2O                  : ARR2(5.5e-12_dp, 2000.0_dp, TEMP);
 { 45:007 } O+OH=HO2{+O2}                  : ARR2(2.2e-11_dp, -120.0_dp, TEMP);
 { 46:008 } O+HO2=OH{+O2}                  : ARR2(3.0e-11_dp, -200.0_dp, TEMP);
 { 47:009 } OH+O3=HO2{+O2}                 : ARR2(1.7e-12_dp, 940.0_dp, TEMP);
 { 48:010 } HO2+O3=OH{+O2+O2}              : ARR2(1.0e-14_dp, 490.0_dp, TEMP);
 { 49:011 } HO2+HO2+H2O=H2O2               : usr9( temp, c_m, c_h2o ) ;
 { 50:012 } H2O2+OH=HO2+H2O                : ARR2(2.9e-12_dp, 160.0_dp, TEMP);
 { 51:013 } OH+HO2=H2O{+O2}                : ARR2(4.8e-11_dp, -250.0_dp, TEMP);
 { 52:014 } OH+OH=H2O+O                    : ARR2(4.2e-12_dp, 240.0_dp, TEMP);
 { 53:015 } OH+OH{+M}=H2O2{+M}             : TROE( 6.90e-31_dp , 1.0_dp , 2.60e-11_dp , 0.0_dp , TEMP, C_M) ;
 { 54:016 } N2O+O1D_CB4=NO+NO              : 6.7e-11_dp ;
 { 55:017 } N2O+O1D_CB4=M{=O2+N2}          : 4.9e-11_dp ;
 { 56:018 } NO+HO2=NO2+OH                  : ARR2(3.5e-12_dp, -250.0_dp, TEMP) ;
 { 57:019 } O3+NO=NO2{+O2}                 : ARR2(3.0e-12_dp, 1500.0_dp, TEMP);
 { 58:020 } O+NO2=NO{+O2}                  : ARR2(5.6e-12_dp, -180.0_dp, TEMP);
 { 59:021 } O3+NO2=NO3{+O2}                : ARR2(1.2e-13_dp, 2450.0_dp, TEMP);
 { 60:022 } NO3+HO2=OH+NO2                 : ARR2(2.3e-12_dp, -170.0_dp, TEMP);
 { 61:023 } NO3+NO2{+M}=N2O5{+M}           : TROE( 2.e-30_dp , 4.4_dp , 1.4e-12_dp , .7_dp , TEMP, C_M) ;
 { 62:024 } N2O5{+M}=NO2+NO3{+M}           : TROEE(3.333e26_dp,10900._dp, 2.2e-30_dp , 4.4_dp , 1.4e-12_dp , .7_dp , TEMP, C_M ) ;
 { 63:025 } OH+NO2{+M}=HNO3{+M}            : TROE( 2.e-30_dp , 3._dp , 2.5e-11_dp , 0._dp , TEMP, C_M) ;
 { 64:026 } OH+HNO3=NO3+H2O                : usr5( TEMP, C_M ) ;
 { 65:027 } NO3+NO=NO2+NO2                 : ARR2(1.5e-11_dp, -170._dp, TEMP);
 { 66:028 } HO2+NO2{+M}=HO2NO2{+M}         : TROE( 1.8e-31_dp , 3.2_dp , 4.7e-12_dp , 1.4_dp , TEMP, C_M) ;
 { 67:029 } OH+HO2NO2=NO2+H2O{+O2}         : ARR2(1.3e-12_dp, -380._dp, TEMP);
 { 68:030 } HO2NO2{+M}=HO2+NO2{+M}         : TROEE( 4.76e26_dp,10900._dp, 1.8e-31_dp , 3.2_dp , 4.7e-12_dp , 1.4_dp , TEMP, C_M ) ;
 { 69:031 } N2O5+M=2.00 HNO3{+M}                : usr16( rh, temp ) ;
 { 70:032 } NO3=HNO3                       : usr17(rh, temp) ;
 { 71:033 } NO2=0.5 OH+0.5 NO+0.5 HNO3     : usr17a(rh, temp) ;
 { 72:034 } CH4+OH=CH3O2+H2O               : ARR2(2.45e-12_dp, 1775.0_dp, TEMP) ;
 { 73:035 } CH4+O1D_CB4=0.75 CH3O2+0.75 OH+0.25 CH2O+0.4 HO2+0.05 H2 : 1.5e-10_dp ;
 { 74:036 } CH3O2+NO=CH2O+NO2+HO2          : ARR2(2.8e-12_dp, -300._dp, TEMP) ;
 { 75:036 } CH3O2+CH3O2=CH2O+CH2O+HO2+HO2  : ARR2(5.e-13_dp, 424._dp, TEMP) ;
 { 76:037 } CH3O2+CH3O2=CH2O+CH3OH         : ARR2(1.9e-14_dp, -706._dp, TEMP) ;
 { 77:038 } CH3O2+HO2=CH3OOH{+O2}          : ARR2(4.1e-13_dp, -750._dp, TEMP) ;
 { 78:039 } CH3OOH+OH=0.7 CH3O2+0.3 OH+0.3 CH2O+H2O : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 { 79:040 } CH2O+NO3=CO+HO2+HNO3           : ARR2( 6.e-13_dp, 2058._dp, TEMP) ;
 { 80:041 } CH2O+OH=CO+HO2+H2O             : 9.e-12_dp ;
 { 81:042 } CO+OH=HO2{+CO2}                : usr8(temp, c_m) ;
 { 82:043 } C2H4+OH{+M}=0.75 EO2+0.5 CH2O+0.25 HO2{+M}          : TROE( 1.e-28_dp , .8_dp , 8.8e-12_dp , 0._dp , TEMP, C_M) ;
 { 83:044 } C2H4+O3=CH2O+0.12 HO2+0.5 CO+0.12 OH+0.25 CH3COOH : ARR2(1.2e-14_dp, 2630._dp, TEMP) ;
 { 84:045 } SO2+OH=SO4                     : usr23( TEMP, C_M ) ;
 { 85:046 } GLYOXAL+OH=HO2+CO{+CO2}        : 1.1e-11_dp ;
 { 86:047 } EO2+NO=EO+NO2                  : ARR2(4.2e-12_dp, -180._dp, TEMP) ;
 { 87:048 } EO+M{=O2}=GLYALD+HO2             : 1e-14_dp ;
 { 88:049 } EO=2 CH2O+HO2                  : ARR2(1.6e11_dp, 4150._dp, TEMP) ;
 { 89:050 } C2H6+OH=C2H5O2{+H2O}           : ARR2(8.7e-12_dp, 1070._dp, TEMP) ;
 { 90:051 } C2H5O2+NO=CH3CHO+HO2+NO2       : ARR2(2.6e-12_dp, -365._dp, TEMP) ;
 { 91:052 } C2H5O2+HO2=C2H5OOH{+O2}        : ARR2(7.5e-13_dp, -700._dp, TEMP) ;
 { 92:053 } C2H5O2+CH3O2=.7 CH2O+.8 CH3CHO+.3 CH3OH+.2 C2H5OH+HO2   : 2.e-13_dp ;
 { 93:054 } C2H5OOH+OH=.5 C2H5O2+.5 CH3CHO+.5 OH   : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 { 94:055 } C3H6+OH{+M}=PO2{+M}            : JPL_TROE( 8.0e-27_dp , 3.5_dp , 3.e-11_dp , 0._dp , .5_dp, TEMP, C_M) ;
 { 95:056 } C3H6+O3=.54 CH2O+.19 HO2+.33 OH+.08 CH4+.56 CO+.5 CH3CHO+.31 CH3O2+.25 CH3COOH :  ARR2(6.5e-15_dp, 1900._dp, TEMP) ;
 { 96:057 } C3H6+NO3=ONIT                  :  ARR2(4.6e-13_dp, 1156._dp, TEMP) ;
 { 97:058 } PO2+NO=CH3CHO+CH2O+HO2+NO2     :  ARR2(4.2e-12_dp, -180._dp, TEMP) ;
 { 98:059 } PO2+HO2=POOH{+O2}              :  ARR2(7.5e-13_dp, -700._dp, TEMP) ;
 { 99:060 } POOH+OH=.5 PO2+.5 OH+.5 HYAC   :  ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 {100:061 } CH3CHO+OH=CH3CO3{+H2O}         :  ARR2(5.6e-12_dp, -270._dp, TEMP) ;
 {101:062 } CH3CHO+NO3=CH3CO3+HNO3         :  ARR2(1.4e-12_dp, 1900._dp, TEMP) ;
 {102:063 } CH3CO3+NO=CH3O2+NO2{+CO2}      :  ARR2(8.1e-12_dp, -270._dp, TEMP) ;
 {103:064 } CH3CO3+NO2{+M}=PAN{+M}         :  TROE( 8.5e-29_dp , 6.5_dp , 1.1e-11_dp , 1._dp , TEMP, C_M) ;
 {104:065 } CH3CO3+HO2=.75 CH3COOOH+.25 CH3COOH+.25 O3  :  ARR2(4.3e-13_dp, -1040._dp, TEMP) ;
 {105:066 } CH3CO3+CH3O2=.9 CH3O2+.9 HO2+.1 CH3COOH+CH2O{+0.5 CO2}  :  ARR2(2.e-12_dp, -500._dp, TEMP) ;
 {106:067 } CH3COOOH+OH=.5 CH3CO3+.5 CH2O{+H2O+0.5 CO2}  : 1.e-12_dp ;
 {107:068 } PAN{+M}=CH3CO3+NO2{+M}         : TROEE( 1.111e28_dp,14000._dp,8.5e-29_dp , 6.5_dp , 1.1e-11_dp , 0._dp , TEMP, C_M) ;
 {108:069 } CH3CO3+CH3CO3=2 CH3O2{+2 CO2}  : ARR2(2.5e-12_dp, -500._dp, TEMP) ;
 {109:070 } C3H8+OH=C3H7O2{+H2O}           : ARR2(1.e-11_dp, 660._dp, TEMP) ;
 {110:071 } C3H7O2+NO=.82 CH3COCH3+.27 CH3CHO+NO2+HO2  : ARR2(4.e-12_dp, -180._dp, TEMP) ;
 {111:072 } C3H7O2+HO2=C3H7OOH{+O2}        : ARR2(7.5e-13_dp, -700._dp, TEMP) ;
 {112:073 } C3H7O2+CH3O2=CH2O+HO2+.82 CH3COCH3  : ARR2(3.75e-13_dp, 40._dp, TEMP) ;
 {113:074 } C3H7OOH+OH=C3H7O2{+H2O}        : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 {114:075 } CH3COCH3+OH=RO2{+H2O}          : 3.82e-11_dp*exp( -2000._dp/temp )+1.33e-13_dp ;
 {115:076 } RO2+NO=CH3CO3+CH2O+NO2         : ARR2(2.9e-12_dp, -300._dp, TEMP) ;
 {116:077 } RO2+HO2=ROOH{+O2}              : ARR2(8.6e-13_dp, -700._dp, TEMP) ;
 {117:078 } RO2+CH3O2=.3 CH3CO3+.8 CH2O+.3 HO2+.2 HYAC+.5 CH3COCHO+.5 CH3OH : ARR2(2.e-12_dp, -500._dp, TEMP) ;
 {118:079 } ROOH+OH=RO2{+H2O}              : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 {119:080 } BIGENE+OH=ENEO2                : 5.4e-11_dp ;
 {120:081 } ENEO2+NO=CH3CHO+.5 CH2O+.5 CH3COCH3+HO2+NO2 : ARR2(4.2e-12_dp, -180._dp, TEMP) ;
 {121:082 } BIGALK+OH=ALKO2                : 3.5e-12_dp ;
 {122:083 } ALKO2+NO=.4 CH3CHO+.1 CH2O+.25 CH3COCH3+.9 HO2+.75 MEK+.9 NO2+.1 ONIT : ARR2(4.2e-12_dp, -180._dp, TEMP) ;
 {123:084 } ALKO2+HO2=ALKOOH               : ARR2(7.5e-13_dp, -700._dp, TEMP) ;
 {124:085 } ALKOOH+OH=ALKO2                : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 {125:086 } ONIT+OH=NO2+CH3COCHO           : 6.8e-13_dp ;
 {126:087 } MEK+OH=MEKO2                   : ARR2(2.3e-12_dp, 170._dp, TEMP) ;
 {127:088 } MEKO2+NO=CH3CO3+CH3CHO+NO2     : ARR2(4.2e-12_dp, -180._dp, TEMP) ;
 {128:089 } MEKO2+HO2=MEKOOH               : ARR2(7.5e-13_dp, -700._dp, TEMP) ;
 {129:090 } MEKOOH+OH=MEKO2                : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 {130:091 } TOLUENE+OH=.25 CRESOL+.25 HO2+.7 TOLO2  : ARR2(1.7e-12_dp, -352._dp, TEMP) ;
 {131:092 } CRESOL+OH=XOH                  : 3.e-12_dp ;
 {132:093 } XOH+NO2=.7 NO2+.7 BIGALD+.7 HO2 : 1.e-11_dp ;
 {133:094 } TOLO2+NO=.45 GLYOXAL+.45 CH3COCHO+.9 BIGALD+.9 NO2+.9 HO2  : ARR2(4.2e-12_dp, -180._dp, TEMP) ;
 {134:095 } TOLO2+HO2=TOLOOH               : ARR2(7.5e-13_dp, -700._dp, TEMP) ;
 {135:096 } TOLOOH+OH=TOLO2                : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 {136:097 } ISOP+OH=ISOPO2                 : ARR2(2.54e-11_dp, -410._dp, TEMP) ;
 {137:098 } ISOP+O3=.4 MACR+.2 MVK+.07 C3H6+.27 OH+.06 HO2+.6 CH2O+.3 CO+.1 O3+.2 MCO3+.2 CH3COOH  : ARR2(1.05e-14_dp, 2000._dp, TEMP) ;
 {138:099 } ISOPO2+NO=.08 ONITR+.92 NO2+.23 MACR+.32 MVK+.37 HYDRALD+.55 CH2O+HO2 : ARR2(4.4e-12_dp, -180._dp, TEMP) ;
 {139:100 } ISOPO2+NO3=HO2+NO2+.6 CH2O+.25 MACR+.35 MVK+.4 HYDRALD : 2.4e-12_dp ;
 {140:101 } ISOPO2+HO2=ISOPOOH             : ARR2(8.e-13_dp, -700._dp, TEMP) ;
 {141:102 } ISOPOOH+OH=.5 XO2+.5 ISOPO2    : ARR2(1.52e-11_dp, -200._dp, TEMP) ;
 {142:103 } ISOPO2+CH3O2=.25 CH3OH+HO2+1.2 CH2O+.19 MACR+.26 MVK+.3 HYDRALD : ARR2(5.e-13_dp, -400._dp, TEMP) ;
 {143:104 } ISOPO2+CH3CO3=CH3O2+HO2+0.6 CH2O+.25 MACR+.35 MVK+.4 HYDRALD{+CO2} : 1.4e-11_dp ;
 {144:105 } MVK+OH=MACRO2                  : ARR2(4.13e-12_dp, -452._dp, TEMP) ;
 {145:106 } MVK+O3=.8 CH2O+.95 CH3COCHO+.08 OH+.2 O3+.06 HO2+.05 CO+.04 CH3CHO : ARR2(7.52e-16_dp, 1521._dp, TEMP) ;
 {146:107 } MACR+OH=.5 MACRO2+.5 MCO3{+0.5 H2O} : ARR2(1.86e-11_dp, -175._dp, TEMP) ;
 {147:108 } MACR+O3=.8 CH3COCHO+.275 HO2+.2 CO+.7 CH2O+.215 OH+.2 O3 : ARR2(4.4e-15_dp, 2500._dp, TEMP) ;
 {148:109 } MACRO2+NO=NO2+.47 HO2+.25 CH2O+.25 CH3COCHO+.53 CH3CO3+.53 GLYALD+.22 HYAC+.22 CO : ARR2(2.7e-12_dp, -360._dp, TEMP) ;
 {149:110 } MACRO2+NO=.8 ONITR             : ARR2(1.3e-13_dp, -360._dp, TEMP) ;
 {150:110 } MACRO2+NO3=NO2+.47 HO2+.25 CH2O+.25 CH3COCHO+.22 CO+.53 GLYALD+.22 HYAC+.53 CH3CO3  : 2.4e-12_dp ;
 {151:111 } MACRO2+HO2=MACROOH             : ARR2(8.e-13_dp, -700._dp, TEMP) ;
 {152:112 } MACRO2+CH3O2=.73 HO2+.88 CH2O+.11 CO+.24 CH3COCHO+.26 GLYALD+.26 CH3CO3+.25 CH3OH+.23 HYAC  : ARR2(5.e-13_dp, -400._dp, TEMP) ;
 {153:113 } MACRO2+CH3CO3=.25 CH3COCHO+CH3O2+.22 CO+.47 HO2+.53 GLYALD+.22 HYAC+.25 CH2O+.53 CH3CO3{+CO2}  : 1.4e-11_dp ;
 {154:114 } MACROOH+OH=.5 MCO3+.2 MACRO2+.1 OH+.2 HO2 : ARR2(2.3e-11_dp, -200._dp, TEMP) ;
 {155:115 } MCO3+NO=NO2+CH2O+CH3CO3{+CO2}  : ARR2(5.3e-12_dp, -360._dp, TEMP) ;
 {156:116 } MCO3+NO3=NO2+CH2O+CH3CO3{+CO2} : 5.e-12_dp ;
 {157:117 } MCO3+HO2=.25 O3+.25 CH3COOH+.75 CH3COOOH {+0.75 O2} : ARR2(4.3e-13_dp, -1040._dp, TEMP) ;
 {158:118 } MCO3+CH3O2=2 CH2O+HO2+CH3CO3{+CO2} : ARR2(2.e-12_dp, -500._dp, TEMP) ;
 {159:119 } MCO3+CH3CO3=CH3O2+CH2O+CH3CO3{+2 CO2} : ARR2(4.6e-12_dp, -530._dp, TEMP) ;
 {160:120 } MCO3+MCO3=2 CH2O+2 CH3CO3{+2 CO2} : ARR2(2.3e-12_dp, -530._dp, TEMP) ;
 {161:121 } MCO3+NO2+M=MPAN{+M}            : 1.1e-11_dp*300._dp/(temp*c_m) ;
 {162:122 } MPAN+M=MCO3+NO2{+M}            : 1.2221e17_dp*300._dp*exp( -14000._dp/temp )/(temp*c_m) ;
 {163:123 } C10H16+OH=TERPO2               : ARR2(1.2e-11_dp, -444._dp, TEMP) ;
 {164:124 } C10H16+O3=.7 OH+MVK+MACR+HO2   : ARR2(1.e-15_dp, 732._dp, TEMP) ;
 {165:125 } C10H16+NO3=TERPO2+NO2          : ARR2(1.2e-12_dp, -490._dp, TEMP) ;
 {166:126 } TERPO2+NO=.1 CH3COCH3+HO2+MVK+MACR+NO2 : ARR2(4.2e-12_dp, -180._dp, TEMP) ;
 {167:127 } TERPO2+HO2=TERPOOH             : ARR2(7.5e-13_dp, -700._dp, TEMP) ;
 {168:128 } TERPOOH+OH=TERPO2              : ARR2(3.8e-12_dp, -200._dp, TEMP) ;
 {169:129 } CH3COOH+OH=CH3O2{+H2O+CO2}     : 7.e-13_dp ;
 {170:130 } ISOP+NO3=ISOPNO3               : ARR2(3.03e-12_dp, 446._dp, TEMP) ;
 {171:131 } ISOPNO3+NO=1.206 NO2+.794 HO2+.072 CH2O+.167 MACR+.039 MVK+.794 ONITR  : ARR2(2.7e-12_dp, -360._dp, TEMP) ;
 {172:132 } ISOPNO3+NO3=1.206 NO2+.072 CH2O+.167 MACR+.039 MVK+.794 ONITR+.794 HO2 : 2.4e-12_dp ;
 {173:133 } ISOPNO3+HO2=.206 NO2+.206 OH+.206 CH2O+.167 MACR+.039 MVK+.794 ONITR : ARR2(8.e-13_dp, -700._dp, TEMP) ;
 {174:134 } CH3COCHO+OH=CH3CO3+CO{+H2O}    : ARR2(8.4e-13_dp, -830._dp, TEMP) ;
 {175:135 } CH3COCHO+NO3=HNO3+CO+CH3CO3    : ARR2(1.4e-12_dp, 1860._dp, TEMP) ;
 {176:136 } ONITR+OH=HYDRALD+HO2+.4 NO2    : 4.5e-11_dp ;
 {177:137 } ONITR+NO3=HYDRALD+HO2+NO2      : ARR2(1.4e-12_dp, 1860._dp, TEMP) ;
 {178:138 } HYDRALD+OH=XO2                 : ARR2(1.86e-11_dp, -175._dp, TEMP) ;
 {179:139 } XO2+NO=NO2+1.5 HO2+CO+.25 HYAC+.25 CH3COCHO+.25 GLYALD : ARR2(2.7e-12_dp, -360._dp, TEMP) ;
 {180:140 } XO2+NO3=NO2+1.5 HO2+CO+.25 HYAC+.25 CH3COCHO+.25 GLYALD  : 2.4e-12_dp ;
 {181:141 } XO2+HO2=XOOH                   : ARR2(8.e-13_dp, -700._dp, TEMP) ;
 {182:142 } XO2+CH3O2=.3 CH3OH+HO2+.7 CH2O+.4 CO+.1 HYAC+.1 CH3COCHO+.1 GLYALD {+CO2}  : ARR2(5.e-13_dp, -400._dp, TEMP) ;
 {183:143 } XO2+CH3CO3=CO+CH3O2+1.5 HO2+.25 HYAC+.25 CH3COCHO+.25 GLYALD  : ARR2(1.3e-12_dp, -640._dp, TEMP) ;
 {184:144 } XOOH+OH=XO2{+H2O}              : ARR2(1.9e-12_dp, -190._dp, TEMP) ;
 {185:145 } XOOH+OH=OH{+H2O}               : THERMAL_T2(7.69e-17_dp, -253._dp, TEMP) ;
 {186:146 } CH3OH+OH=HO2+CH2O              : ARR2(7.3e-12_dp, 620._dp, TEMP) ;
 {187:147 } C2H5OH+OH=HO2+CH3CHO           : ARR2(6.9e-12_dp, 230._dp, TEMP) ;
 {188:148 } MPAN+OH=.5 HYAC+.5 NO3+.5 CH2O+.5 HO2 {+0.5 CO2} : JPL_TROE( 8.e-27_dp , 3.5_dp , 3.e-11_dp , 0.0_dp , .5_dp, TEMP, C_M) ;
 {189:149 } PAN+OH=CH2O+NO3{+CO2}          : 4.e-14_dp ;
 {190:150 } HYAC+OH=CH3COCHO+HO2           : 3.e-12_dp ;
 {191:151 } GLYALD+OH=HO2+.2 GLYOXAL+.8 CH2O {+0.8 CO2} : 1.e-11_dp ;
 {192:152 } DMS+OH=SO2                     : ARR2(9.6e-12_dp, 234._dp, TEMP) ;
 {193:153 } DMS+OH=.5 SO2+.5 HO2           : usr24( temp, c_m ) ;
 {194:154 } DMS+NO3=SO2+HNO3               : ARR2(1.9e-13_dp, -520._dp, TEMP) ;
 {195:155 } NH3+OH=M                       : ARR2(1.7e-12_dp, 710._dp, TEMP) ;
 {196:156 } HO2=.5 H2O2                    : usr26( rh, temp ) ;
 {197:157 } C2H5O2+C2H5O2=1.6 CH3CHO+1.2 HO2+.4 C2H5OH  : 6.8e-14_dp ;
